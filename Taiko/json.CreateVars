<?xml version="1.0" encoding="UTF-8"?>
<fmxmlsnippet type="FMObjectList">
  <CustomFunction id="73" functionArity="2" visible="True" parameters="json;namespace" name="json.CreateVars">
    <Calculation><![CDATA[/**
 * @SIGNATURE:
 * json.CreateVars ( json ; namespace )
 *
 * @PARAMETERS:
 * json - JSON data
 * namespace - Prefix for the variables that are to be created; can be left blank
 *
 * @HISTORY:
 * CREATED on 2017-12-08 by Mislav Kos <mkos@soliantconsulting.com>
 * MODIFIED on 2018-02-18 by Mislav Kos <mkos@soliantconsulting.com> - corrected typecasting portion of the custom function
 * MODIFIED on 2018-04-01 by Bob Bowers <bbowers@soliantconsulting.com> - allow for specifying global rather than local variables with an asterisk prefix for the json element
 * MODIFIED on 2021-03-28 by Marcelo Piñeyro <mpineyro@soliantconsulting.com> - return error _ERROR_INVALID_PARAM when the parameter can't be parsed
 * MODIFIED on 2024-09-16 by Marcelo Piñeyro <mpineyro@soliantconsulting.com> - changed name from #Json.CreateVarsFromKeys to #Json.CreateVarsPrivate, so it's clear that this is one of the private functions in the clew suite of custom functions
* MODIFIED on 2024-10-21 by Marcelo Piñeyro <mpineyro@soliantconsulting.com> - changed name from #Json.CreateVarsPrivate to #Json.CreateVars
* MODIFIED on 2024-11-06 by Marcelo Piñeyro - replaced "#Json." prefix with "json." in the function's name
* MODIFIED on 2025-12-16  by roberto@taikosolutions.com - Cambiada comprobación de date para que no recoja como date textos que se parezcan a fechas.
 *
 * @PURPOSE:
 * Create local variables for all keys belonging to the JSON document's root node.
 * For example, given the following JSON, this custom function will create $id and $color variables: { "id" : "123", "color" : "blue" }
 *
 * @RESULT:
 * This custom function will return an error code as a result.
 * 0 means success or empty JSON.
 * _Error_INVALID_PARAM means invalid json
 * 1204 means one of the JSON root keys did not conform to the FileMaker naming restrictions for variable and fields.
 * Pre-16 clients will return "?".
 *
 * @ERRORS:
 * Errors will be indicated in the custom function result using standard FileMaker error codes: https://fmhelp.filemaker.com/help/16/fmp/en/#page/FMP_Help%2Ferror-codes.html.
 *
 * @NOTES:
 * Keys must be named following the same naming restrictions as FileMaker variables and fields: https://fmhelp.filemaker.com/help/16/fmp/en/index.html#page/FMP_Help%2Fnaming-fields.html.
 * 
 * @DEPENDENCIES:
 * Does not require any other custom functions. Requires v16 or later client. Pre-16 clients will return "?".
 */

Case ( 
	IsEmpty ( json ) ; 0 ; // If JSON is empty, return 0 ("no error")
	
	Left ( JSONFormatElements ( json ) ; 1 ) = "?" ; _error_INVALID_PARAM ;
	
	Let ( [ 
		~keys = JSONListKeys ( json ; "." ) ; // Get keys from JSON document's root node
		~key = GetValue ( ~keys ; ValueCount ( ~keys ) ) // Process keys starting with the last one; otherwise JSON arrays won't process correctly
	] ; 
		If ( 
			IsEmpty ( ~key ) ; 0 ; // If the JSON document's root node doesn't contain any keys, return 0 ("no error")
			
			// Create variable based on key, then delete key from JSON, and then recursively call CF again to process remaining keys
			Let ( [ 
				// Get value for key
				~value = JSONGetElement ( json ; ~key ) ; 
				
				// Ensure correct typecasting of value; without this, everything would get typecast as text
				// This next section was taken from the the # custom function and (I think) was written by Daniel Smith (github.com/dansmith65) and Jeremy Bante (github.com/jbante)
				// See https://github.com/filemakerstandards/fmpstandards/blob/master/Functions/%23Name-Value/%23.fmfn
				~text = GetAsText ( ~value ) ; 
				~number = GetAsNumber ( ~value ) ; 
				~validDate = IsValid ( GetAsDate ( ~text ) ) and not IsEmpty ( ~number )  and GetAsDate ( ~text )  = ~text ; 
				~validTime = IsValid ( GetAsTime ( ~text ) ) and Position ( ~text ; ":" ; 1 ; 1 ) > 0 and not IsEmpty ( ~number ) ;
				~value = Case ( 
					~value = "" or ~value = "?" ; Quote ( ~value ) ;
					~validTime and ~validDate ; "GetAsTimestamp ( " & Quote ( ~value ) & " )" ;
					~validTime ; "GetAsTime ( " & Quote ( ~value ) & " )" ;
					~validDate ; "GetAsDate ( " & Quote ( ~value ) & " )" ;
					~text ≠ GetAsText ( ~number ) ; Quote ( ~value ) ; 
					~number
				) ; 

				
				// Create variable based on key and value (and namespace)

                                ~newKey = Substitute ( ~key ; "*" ; "$" ) ;  // allows for specifying global variables by beginning key with asterisk

				~error = EvaluationError ( Evaluate ( "Let ( $" & namespace & ~newKey & " = " & ~value & " ; \"\" ) " ) ) 
			] ; 
				If ( 
					~error ≠ 0 ; ~error ; // If we encountered an error, return the error code and don't bother processing the rest of the keys
					Let ( 
						json = JSONDeleteElement ( json ; ~key ) ; // Delete key from JSON
						json.CreateVars ( json ; namespace ) // Recursively call custom function to process remaining keys
					)
				)
			)
		)
	)
)]]></Calculation>
  </CustomFunction>
</fmxmlsnippet>
